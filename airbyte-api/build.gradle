import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "io.airbyte.gradle.jvm.lib"
    id "io.airbyte.gradle.publish"
    id "org.openapi.generator"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
}

def specFile = "$projectDir/src/main/openapi/config.yaml"
def airbyteApiSpecFile = "$projectDir/src/main/openapi/api.yaml"
def airbyteApiProblemsSpecFile = "$projectDir/src/main/openapi/api-problems.yaml"
def airbyteApiSpecTemplateDirApi = "$projectDir/src/main/resources/templates/jaxrs-spec-api"
def publicApiSpecTemplateDirApi = "$projectDir/src/main/resources/templates/jaxrs-spec-api/public_api"
def workloadSpecFile = "$projectDir/src/main/openapi/workload-openapi.yaml"
def connectorBuilderServerSpecFile = project(":airbyte-connector-builder-server").file("src/main/openapi/openapi.yaml").getPath()

def genApiServer = tasks.register("generateApiServer", GenerateTask) {
    def serverOutputDir = "${getLayout().buildDirectory.get()}/generated/api/server"

    inputs.file specFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = specFile
    outputDir = serverOutputDir

    apiPackage = "io.airbyte.api.generated"
    invokerPackage = "io.airbyte.api.invoker.generated"
    modelPackage = "io.airbyte.api.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'DeclarativeManifest'               : 'com.fasterxml.jackson.databind.JsonNode',
            'SecretPersistenceConfigurationJson': 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            dateLibrary                   : "java8",
            generatePom                   : "false",
            interfaceOnly                 : "true",
            /*
            JAX-RS generator does not respect nullable properties defined in the OpenApi Spec.
            It means that if a field is not nullable but not set it is still returning a null value for this field in the serialized json.
            The below Jackson annotation is made to only keep non null values in serialized json.
            We are not yet using nullable=true properties in our OpenApi so this is a valid workaround at the moment to circumvent the default JAX-RS behavior described above.
            Feel free to read the conversation on https://github.com/airbytehq/airbyte/pull/13370 for more details.
            */
            additionalModelTypeAnnotations: "\n@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",

            // Generate separate classes for each endpoint "domain"
            useTags                       : "true",
    ]

    doLast {
        // Remove unnecessary invoker classes to avoid Micronaut picking them up and registering them as beans
        delete("${serverOutputDir}/src/gen/java/${invokerPackage.get().replaceAll("\\.", "/").replaceAll("-","_")}")
        // Clean up any javax references
        [apiPackage.get(), modelPackage.get()].each { sourceDir ->
            updateToJakartaApi(file("${serverOutputDir}/src/gen/java/${sourceDir.replaceAll('\\.', '/').replaceAll('-', '_')}"))
        }
    }
}

def genApiClient = tasks.register("genApiClient", GenerateTask) {
    def clientOutputDir = "${getLayout().buildDirectory.get()}/generated/api/client"

    inputs.file specFile
    outputs.dir clientOutputDir

    generatorName = "kotlin"
    inputSpec = specFile
    outputDir = clientOutputDir

    apiPackage = "io.airbyte.api.client.generated"
    invokerPackage = "io.airbyte.api.client.invoker.generated"
    modelPackage = "io.airbyte.api.client.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'SecretPersistenceConfigurationJson': 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            enumPropertyNaming  : "UPPERCASE",
            generatePom         : "false",
            interfaceOnly       : "true"
    ]

    doLast {
        def apiClientPath = "${clientOutputDir}/src/main/kotlin/org/openapitools/client/infrastructure/ApiClient.kt"
        updateApiClientWithFailsafe(apiClientPath)
        updateDomainClientsWithFailsafe("${clientOutputDir}/src/main/kotlin/io/airbyte/api/client/generated")

        // a JsonNode adapter needs to be added to the kotlin client's serializer to handle JsonNode fields in requests
        updateApiClientSerializerWithJsonNodeAdapter("${clientOutputDir}/src/main/kotlin/org/openapitools/client/infrastructure/Serializer.kt")
    }
}

def genApiDocs = tasks.register("generateApiDocs", GenerateTask) {
    def docsOutputDir = "${getLayout().buildDirectory.get()}/generated/api/docs"

    generatorName = "html"
    inputSpec = specFile
    outputDir = docsOutputDir

    apiPackage = "io.airbyte.api.client.generated"
    invokerPackage = "io.airbyte.api.client.invoker.generated"
    modelPackage = "io.airbyte.api.client.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            dateLibrary  : "java8",
            generatePom  : "false",
            interfaceOnly: "true"
    ]
}

def genPublicApiServer = tasks.register('generatePublicApiServer', GenerateTask) {
    def serverOutputDir = "${getLayout().buildDirectory.get()}/generated/public_api/server"

    inputs.file specFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = specFile
    outputDir = serverOutputDir
    templateDir = publicApiSpecTemplateDirApi

    apiPackage = "io.airbyte.public-api.generated"
    invokerPackage = "io.airbyte.public-api.invoker.generated"
    modelPackage = "io.airbyte.public-api.model.generated"

    generateApiDocumentation = false

    configOptions = [
            dateLibrary                   : "java8",
            generatePom                   : "false",
            interfaceOnly                 : "true",
            returnResponse                : "true",
            useBeanValidation             : "true",
            performBeanValidation         : "true",
            additionalModelTypeAnnotations: "@io.micronaut.core.annotation.Introspected",
            additionalEnumTypeAnnotations : "@io.micronaut.core.annotation.Introspected",
            useTags                       : "true"
    ]

    schemaMappings = [
            'SourceConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'OAuthInputConfiguration'      : 'com.fasterxml.jackson.databind.JsonNode',
            'OAuthCredentialsConfiguration': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'     : 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    doLast {
        // Remove unnecessary invoker classes to avoid Micronaut picking them up and registering them as beans
        delete("${serverOutputDir}/src/gen/java/${invokerPackage.get().replaceAll("\\.", "/").replaceAll("-","_")}")
        // Clean up any javax references
        [apiPackage.get(), modelPackage.get()].each { sourceDir ->
            updateToJakartaApi(file("${serverOutputDir}/src/gen/java/${sourceDir.replaceAll('\\.', '/').replaceAll('-', '_')}"))
        }
    }

}

def genAirbyteApiServer = tasks.register('generateAirbyteApiServer', GenerateTask) {
    def serverOutputDir = "${getLayout().buildDirectory.get()}/generated/airbyte_api/server"

    inputs.file airbyteApiSpecFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = airbyteApiSpecFile
    outputDir = serverOutputDir
    templateDir = airbyteApiSpecTemplateDirApi

    apiPackage = "io.airbyte.airbyte-api.generated"
    invokerPackage = "io.airbyte.airbyte-api.invoker.generated"
    modelPackage = "io.airbyte.airbyte-api.model.generated"

    generateApiDocumentation = false

    configOptions = [
            dateLibrary                   : "java8",
            generatePom                   : "false",
            interfaceOnly                 : "true",
            returnResponse                : "true",
            useBeanValidation             : "true",
            performBeanValidation         : "true",
            additionalModelTypeAnnotations: "@io.micronaut.core.annotation.Introspected",
            additionalEnumTypeAnnotations : "@io.micronaut.core.annotation.Introspected",
    ]

    schemaMappings = [
            'SourceConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'OAuthInputConfiguration'      : 'com.fasterxml.jackson.databind.JsonNode',
            'OAuthCredentialsConfiguration': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'     : 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    doLast {
        // Remove unnecessary invoker classes to avoid Micronaut picking them up and registering them as beans
        delete("${serverOutputDir}/src/gen/java/${invokerPackage.get().replaceAll("\\.", "/").replaceAll("-","_")}")
        // Clean up any javax references
        [apiPackage.get(), modelPackage.get()].each { sourceDir ->
            updateToJakartaApi(file("${serverOutputDir}/src/gen/java/${sourceDir.replaceAll('\\.', '/').replaceAll('-', '_')}"))
        }
    }
}

def genAirbyteApiProblems = tasks.register("genAirbyteApiProblems", GenerateTask) {
    def serverOutputDir = "${getLayout().buildDirectory.get()}/generated/api/problems"

    inputs.file airbyteApiProblemsSpecFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = airbyteApiProblemsSpecFile
    outputDir = serverOutputDir
    templateDir = publicApiSpecTemplateDirApi

    packageName = "io.airbyte.api.problems"
    invokerPackage = "io.airbyte.api.problems.invoker.generated"
    modelPackage = "io.airbyte.api.problems.model.generated"

    generateApiDocumentation = false

    configOptions = [
        enumPropertyNaming  : "UPPERCASE",
        generatePom         : "false",
        interfaceOnly       : "true",
    ]

    doLast {
        // Remove unnecessary invoker classes to avoid Micronaut picking them up and registering them as beans
        delete("${serverOutputDir}/src/gen/java/${invokerPackage.get().replaceAll("\\.", "/").replaceAll("-","_")}")

        def generatedModelPath = "${serverOutputDir}/src/gen/java/${modelPackage.get().replaceAll('\\.', '/').replaceAll('-', '_')}"
        updateToJakartaApi(file(generatedModelPath))
        generateProblemThrowables(generatedModelPath)
    }
}

// TODO: Linked to document okhhtp
def genWorkloadApiClient = tasks.register("genWorkloadApiClient", GenerateTask) {
    def clientOutputDir = "${getLayout().buildDirectory.get()}/generated/workloadapi/client"

    inputs.file workloadSpecFile
    outputs.dir clientOutputDir

    generatorName = "kotlin"
    inputSpec = workloadSpecFile
    outputDir = clientOutputDir

    apiPackage = "io.airbyte.workload.api.client.generated"
    packageName = "io.airbyte.workload.api.client.generated"
    modelPackage = "io.airbyte.workload.api.client.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            enumPropertyNaming  : "UPPERCASE",
            generatePom         : "false",
            interfaceOnly       : "true"
    ]

    doLast {
        def apiClientPath = "${clientOutputDir}/src/main/kotlin/io/airbyte/workload/api/client/generated/infrastructure/ApiClient.kt"
        updateApiClientWithFailsafe(apiClientPath)
        def generatedDomainClientsPath = "${clientOutputDir}/src/main/kotlin/io/airbyte/workload/api/client/generated"
        updateDomainClientsWithFailsafe(generatedDomainClientsPath)
        // the kotlin client (as opposed to the java client) doesn't include the response body in the exception message.
        updateDomainClientsToIncludeHttpResponseBodyOnClientException(generatedDomainClientsPath)
    }

    dependsOn(':airbyte-workload-api-server:compileKotlin', 'genApiClient')
}

def genConnectorBuilderServerApiClient = tasks.register("genConnectorBuilderServerApiClient", GenerateTask) {
    def clientOutputDir = "${getLayout().buildDirectory.get()}/generated/connectorbuilderserverapi/client"

    inputs.file connectorBuilderServerSpecFile
    outputs.dir clientOutputDir

    generatorName = "kotlin"
    inputSpec = connectorBuilderServerSpecFile
    outputDir = clientOutputDir

    apiPackage = "io.airbyte.connectorbuilderserver.api.client.generated"
    invokerPackage = "io.airbyte.connectorbuilderserver.api.client.invoker.generated"
    modelPackage = "io.airbyte.connectorbuilderserver.api.client.model.generated"

    schemaMappings = [
            'ConnectorConfig'   : 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorManifest' : 'com.fasterxml.jackson.databind.JsonNode',
            'ConnectorBuilderProjectTestingValues': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            enumPropertyNaming  : "UPPERCASE",
            generatePom         : "false",
            interfaceOnly       : "true"
    ]

    doLast {
        // Delete file generated by the client task
        file("${clientOutputDir}/src/main/kotlin/org").deleteDir()

        def generatedDomainClientsPath = "${clientOutputDir}/src/main/kotlin/io/airbyte/connectorbuilderserver/api/client/generated"
        updateDomainClientsWithFailsafe(generatedDomainClientsPath)
        // the kotlin client (as opposed to the java client) doesn't include the response body in the exception message.
        updateDomainClientsToIncludeHttpResponseBodyOnClientException(generatedDomainClientsPath)
    }

    dependsOn('genApiClient')
}


compileJava.dependsOn genApiDocs, genApiClient, genApiServer, genAirbyteApiServer, genPublicApiServer

kapt {
    correctErrorTypes true
}

// uses afterEvaluate because at configuration time, the kaptGenerateStubsKotlin task does not exist.
afterEvaluate {
    tasks.named('kaptGenerateStubsKotlin').configure {
        mustRunAfter genApiDocs, genApiClient, genApiServer, genAirbyteApiServer, genPublicApiServer,
                genWorkloadApiClient, genConnectorBuilderServerApiClient, genAirbyteApiProblems
    }
}

tasks.named("compileKotlin") {
    dependsOn tasks.named("genApiClient")
    dependsOn tasks.named("genWorkloadApiClient")
    dependsOn tasks.named("genConnectorBuilderServerApiClient")
    dependsOn tasks.named("genAirbyteApiProblems")
}

dependencies {
    annotationProcessor libs.micronaut.openapi
    kapt libs.micronaut.openapi
    kapt platform(libs.micronaut.platform)
    kapt libs.bundles.micronaut.annotation.processor
    kapt libs.v3.swagger.annotations
    kapt libs.moshi.kotlin

    api libs.bundles.micronaut.annotation
    api libs.micronaut.http
    api libs.bundles.micronaut.metrics
    api libs.failsafe.okhttp
    api libs.okhttp
    api libs.guava
    api libs.java.jwt
    api libs.google.auth.library.oauth2.http
    api libs.kotlin.logging
    api libs.moshi.kotlin
    api project(":airbyte-config:config-models")

    implementation platform(libs.micronaut.platform)
    implementation libs.bundles.micronaut
    implementation libs.commons.io
    implementation libs.jakarta.annotation.api
    implementation libs.jakarta.ws.rs.api
    implementation libs.jakarta.validation.api
    implementation libs.jackson.datatype
    implementation libs.jackson.databind
    implementation libs.openapi.jackson.databind.nullable
    implementation libs.reactor.core
    implementation libs.slf4j.api
    implementation libs.swagger.annotations

    implementation project(":airbyte-commons")

    compileOnly libs.v3.swagger.annotations

    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.bundles.junit
    testImplementation libs.bundles.jackson
    testImplementation libs.assertj.core
    testImplementation libs.junit.pioneer
    testImplementation libs.mockk
    testImplementation libs.kotlin.test.runner.junit5
}

sourceSets {
    main {
        java {
            srcDirs "${project.layout.buildDirectory.get()}/generated/api/server/src/gen/java",
                    "${project.layout.buildDirectory.get()}/generated/airbyte_api/server/src/gen/java",
                    "${project.layout.buildDirectory.get()}/generated/public_api/server/src/gen/java",
                    "${project.layout.buildDirectory.get()}/generated/api/client/src/main/kotlin",
                    "${project.layout.buildDirectory.get()}/generated/workloadapi/client/src/main/kotlin",
                    "${project.layout.buildDirectory.get()}/generated/connectorbuilderserverapi/client/src/main/kotlin",
                    "${project.layout.buildDirectory.get()}/generated/api/problems/src/gen/kotlin",
                    "${project.layout.buildDirectory.get()}/generated/api/problems/src/gen/java",
            "$projectDir/src/main/java"
        }
        resources {
            srcDir "$projectDir/src/main/openapi/"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-parameters'
    ]
}

airbyte {
    spotless {
        excludes = ["src/main/openapi/workload-openapi.yaml"]
    }
}

// Even though Kotlin is excluded on Spotbugs, this project
// still runs into spotbug issues. Working theory is that
// generated code is being picked up. Disable as a short-term fix.
tasks.named("spotbugsMain") {
    enabled = false
}

private def updateApiClientWithFailsafe(def clientPath) {
    /*
     * UPDATE ApiClient.kt to use Failsafe.
     */
    def apiClientFile = file(clientPath)
    def apiClientFileText = apiClientFile.text

    // replace class declaration
    apiClientFileText = apiClientFileText.replace(
            'open class ApiClient(val baseUrl: String, val client: OkHttpClient = defaultClient) {',
            'open class ApiClient(val baseUrl: String, val client: OkHttpClient = defaultClient, val policy : RetryPolicy<Response> = RetryPolicy.ofDefaults()) {')

    // replace execute call
    apiClientFileText = apiClientFileText.replace(
            'val response = client.newCall(request).execute()',
            '''val call = client.newCall(request)
        val failsafeCall = FailsafeCall.with(policy).compose(call)
        val response: Response = failsafeCall.execute()

        return response.use { processResponse(response) }
        }

        protected inline fun <reified T: Any?> processResponse(response: Response): ApiResponse<T?> {''')

    // add imports if not exist
    if (!apiClientFileText.contains("import dev.failsafe.RetryPolicy")) {
        def newImports = '''import dev.failsafe.RetryPolicy
import dev.failsafe.okhttp.FailsafeCall'''
        apiClientFileText = apiClientFileText.replaceFirst('import ', newImports + '\nimport ')

    }
    apiClientFile.write(apiClientFileText)
}

private def generateProblemThrowables(def problemsOutputDir) {
    def dir = file(problemsOutputDir)

    def throwableDir = new File("${getLayout().buildDirectory.get()}/generated/api/problems/src/gen/kotlin/throwable")
    if (!throwableDir.exists()) {
        throwableDir.mkdirs()
    }

    dir.eachFile { errorFile ->
        if (errorFile.name.endsWith('ProblemResponse.java')) {
            def errorFileText = errorFile.text

            def problemName = (errorFileText =~ /public class (\S+)ProblemResponse /)[0][1] as String
            def dataFieldType = (errorFileText =~ /private (@Valid )?\n(\S+) data;/)[0][2] as String
            def dataFieldImport = "import io.airbyte.api.problems.model.generated.${dataFieldType}"

            if (dataFieldType == "Object") {
                dataFieldType = "Any"
                dataFieldImport = ""
            }

            def responseClassName = "${problemName}ProblemResponse"
            def throwableClassName = "${problemName}Problem"

            def template = new File("${projectDir}/src/main/resources/templates/ThrowableProblem.kt.txt")
            def throwableText = template.text
                .replaceAll('<problem-class-name>', responseClassName)
                .replaceAll('<problem-throwable-class-name>', throwableClassName)
                .replaceAll('<problem-data-class-import>', dataFieldImport)
                .replaceAll('<problem-data-class-name>', dataFieldType)

            def throwableFile = new File(throwableDir, "${throwableClassName}.kt")
            throwableFile.write(throwableText)
        }
    }
}

private def updateDomainClientsWithFailsafe(def clientPath) {
    /*
     * UPDATE domain clients to use Failsafe.
     */
    def dir = file(clientPath)
    dir.eachFile { domainClient ->
        if (domainClient.name.endsWith('.kt')) {
            def domainClientFileText = domainClient.text

            // replace class declaration
            domainClientFileText = domainClientFileText.replaceAll(
                    /class (\S+)\(basePath: kotlin.String = defaultBasePath, client: OkHttpClient = ApiClient.defaultClient\) : ApiClient\(basePath, client\)/,
                    'class $1(basePath: kotlin.String = defaultBasePath, client: OkHttpClient = ApiClient.defaultClient, policy : RetryPolicy<okhttp3.Response> = RetryPolicy.ofDefaults()) : ApiClient(basePath, client, policy)'
            )

            // add imports if not exist
            if(!domainClientFileText.contains("import dev.failsafe.RetryPolicy")) {
                def newImports = "import dev.failsafe.RetryPolicy"
                domainClientFileText = domainClientFileText.replaceFirst('import ', newImports + '\nimport ')
            }

            domainClient.write(domainClientFileText)
        }
    }
}

private def updateDomainClientsToIncludeHttpResponseBodyOnClientException(def clientPath) {
    def dir = file(clientPath)
    dir.eachFile { domainClient ->
        if (domainClient.name.endsWith('.kt')) {
            def domainClientFileText = domainClient.text

            domainClientFileText = domainClientFileText.replace(
                    'throw ClientException("Client error : ${localVarError.statusCode} ${localVarError.message.orEmpty()}", localVarError.statusCode, localVarResponse)',
                    'throw ClientException("Client error : ${localVarError.statusCode} ${localVarError.message.orEmpty()} ${localVarError.body ?: ""}", localVarError.statusCode, localVarResponse)')

            domainClient.write(domainClientFileText)
        }
    }
}

private def updateApiClientSerializerWithJsonNodeAdapter(def serializerPath) {
    /*
     * UPDATE Serializer to include JsonNode adapter
     */
    def serializerFile = file(serializerPath)
    def serializerFileText = serializerFile.text

    // add imports if not present
    if (!serializerFileText.contains('import io.airbyte.api.client.infrastructure')) {
        def newImports = [
                'import io.airbyte.api.client.infrastructure.AirbyteCatalogAdapter',
                'import io.airbyte.api.client.infrastructure.AirbyteLocalDateAdapter',
                'import io.airbyte.api.client.infrastructure.ConnectorBuilderPublishRequestBodyAdapter',
                'import io.airbyte.api.client.infrastructure.DestinationSyncModeAdapter',
                'import io.airbyte.api.client.infrastructure.FailureOriginAdapter',
                'import io.airbyte.api.client.infrastructure.FailureTypeAdapter',
                'import io.airbyte.api.client.infrastructure.JsonNodeAdapter',
                'import io.airbyte.api.client.infrastructure.ReplicationStatusAdapter',
                'import io.airbyte.api.client.infrastructure.SyncModeAdapter',
        ]
        serializerFileText = serializerFileText.replaceFirst('import ', "${newImports.join('\n')}\nimport ")
    }

    // add JsonNode adapter to builder
    serializerFileText = serializerFileText.replace("Moshi.Builder()", '''Moshi.Builder()
        .add(AirbyteLocalDateAdapter())
        .add(AirbyteCatalogAdapter())
        .add(ConnectorBuilderPublishRequestBodyAdapter())
        .add(DestinationSyncModeAdapter())
        .add(FailureOriginAdapter())
        .add(FailureTypeAdapter())
        .add(JsonNodeAdapter())
        .add(ReplicationStatusAdapter())
        .add(SyncModeAdapter())''')

    serializerFile.write(serializerFileText)
}

private static def updateToJakartaApi(srcDir) {
    srcDir.eachFileRecurse { file ->
        if(file.isFile()) {
            def contents = file.text
            contents = contents
                    .replaceAll('javax\\.ws\\.rs', 'jakarta.ws.rs')
                    .replaceAll('javax\\.validation', 'jakarta.validation')
                    .replaceAll('javax\\.annotation\\.processing', 'jakarta.annotation')
                    .replaceAll('javax\\.annotation', 'jakarta.annotation')
                    .replaceAll('jakarta\\.annotation\\.processing', 'jakarta.annotation')
            file.write(contents)
        }
    }
}
